plugins {
    id 'java'
    id 'idea'

    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'tk.booky'
version '0.1.0' + (System.getenv('GITHUB_ACTIONS') == 'true' ? '_' + System.getenv('GITHUB_RUN_ID') : '')

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

dependencies {
    implementation 'net.dv8tion:JDA:4.2.0_229'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'com.google.code.gson:gson:2.8.6'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    repositories {
        maven {
            name = 'GitHub'
            url = uri('https://maven.pkg.github.com/booky10/JDABotHelper')

            credentials {
                username = 'booky10'
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId = 'jdabothelper'

            artifact shadowJar { classifier '' }
            artifact sourcesJar { classifier 'sources' }
            artifact javadocJar { classifier 'javadoc' }
        }
    }
}