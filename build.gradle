plugins {
    id 'java'
    id 'idea'

    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'tk.booky'
version '0.1.4'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

dependencies {
    implementation 'net.dv8tion:JDA:4.2.0_229'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'com.google.code.gson:gson:2.8.6'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Bintray']

    pkg {
        repo = 'JDABotHelper'
        name = project.rootProject.name
        publish = true

        vcsUrl = 'https://github.com/booky10/JDABotHelper.git'
        licenses = ['MIT']

        version {
            name = project.version as String
            released = new Date().toString()
            vcsTag = 'v' + (project.version as String)
        }
    }
}

publishing {
    publications {
        Bintray(MavenPublication) {
            artifactId = 'jdabothelper'

            artifact jar { classifier '' }
            artifact shadowJar { classifier 'all' }
            artifact sourcesJar { classifier 'sources' }
            artifact javadocJar { classifier 'javadoc' }
        }
    }
}